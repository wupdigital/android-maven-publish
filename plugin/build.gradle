plugins {
    id 'groovy'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.1'
    id 'com.gradle.plugin-publish' version '0.9.10'
    id 'org.ajoberstar.git-publish' version '0.4.1'
    id 'idea'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply from: '../gradle/shared.gradle'
apply from: '../gradle/integTest.gradle'
apply from: '../gradle/testReport.gradle'

repositories {
    google()
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compileOnly "com.android.tools.build:gradle:$build_tools_version"
    testImplementation 'junit:junit:4.12'
    integTestImplementation "com.android.tools.build:gradle:$build_tools_version"
    integTestImplementation gradleTestKit()
    integTestImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier.set('javadoc')
    from groovydoc.getDestinationDir()
}

Boolean isRelease() {
    !project.version.endsWith('-SNAPSHOT')
}

def scmTag() {
    isRelease() ? project.version : 'master'
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            name 'Bal√°zs Varga'
            email 'balazs.varga@wup.hu'
            organization 'W.UP Ltd.'
            organizationUrl 'http://wup.digital'
        }
    }
    scm {
        connection "scm:git:git://${SCM_URL}.git"
        developerConnection "scm:git:ssh://${SCM_URL}.git"
        url "https://${SCM_URL}/tree/${scmTag()}"
        tag scmTag()
    }
}

artifacts {
    archives sourcesJar, groovydocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            artifact sourcesJar
            artifact groovydocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', DESCRIPTION)
                root.appendNode('name', project.name)
                root.appendNode('url', "https://${SCM_URL}")
                root.children().last() + pomConfig
            }
        }
    }
    if (!isRelease()) {
        repositories {
            maven {
                url = 'https://oss.sonatype.org/content/repositories/snapshots'
                credentials {
                    username = System.getenv('OSS_USER')
                    password = System.getenv('OSS_PASSWORD')
                }
            }
        }
    }
}

def projectTags = ['android', 'gradle-plugin', 'publishing', 'maven-publish', 'aar', 'android-library']

if (isRelease()) {
    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        publications = ['maven']

        dryRun = false
        publish = false

        pkg {
            repo = 'wup-digital-maven'
            name = project.name
            userOrg = 'wupdigital'
            description = DESCRIPTION
            websiteUrl = "https://${SCM_URL}"
            issueTrackerUrl = "https://${SCM_URL}/issues"
            vcsUrl = "https://${SCM_URL}"
            licenses = ['Apache-2.0']
            labels = projectTags

            githubRepo = 'wupdigital/android-maven-publish'
            githubReleaseNotesFile = 'CHANGELOG.md'

            version {
                name = project.version
                vcsTag = scmTag()
                released = new Date()

                gpg {
                    sign = true
                    passphrase = System.getenv('GPG_PASSPHARSE')
                }

                mavenCentralSync {
                    sync = true
                    user = System.getenv('OSS_USER')
                    password = System.getenv('OSS_PASSWORD')
                }
            }
        }
    }
    publish.dependsOn bintrayUpload
    bintrayUpload.dependsOn 'generatePomFileForMavenPublication', sourcesJar, groovydocJar
}

pluginBundle {
    website = "https://${SCM_URL}"
    vcsUrl = "https://${SCM_URL}"
    description = DESCRIPTION
    tags = projectTags

    plugins {
        publishPlugin {
            id = "${project.group}.${project.name}"
            displayName = NAME
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
        version = project.version
    }
}

publishPlugins.enabled = isRelease()

gitPublish {
    repoUri = "https://${SCM_URL}.git".toString()
    branch = 'gh-pages'

    contents {
        from groovydoc.getDestinationDir()
        into "groovydoc${File.separator}${project.version}"
    }

    preserve { include '**/*' }

    commitMessage = "Publish groovydoc for ${project.version} version".toString()
}

gitPublishPush.enabled = isRelease()
